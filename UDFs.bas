Attribute VB_Name = "UDFs"
Option Explicit

''=======================================================
'' Program:     AddWeek
'' Desc:        Adds a week to fill in into the active worksheet
'' Called by:   Directly
'' Call:        AddWeek
'' Arguments:   None
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 14-07-2018   Pieter de Rooij     Dynamically adds a week to the sheet
'' 23-07-2018   Pieter de Rooij     Functionality to undo this macro
''=======================================================
Sub AddWeek()
Attribute AddWeek.VB_Description = "Adds a week to fill in into the template."
Attribute AddWeek.VB_ProcData.VB_Invoke_Func = " \n14"
'
' AddWeek Macro
' Adds a week to the template to fill out.
'

'
    ' Initialize variables to use for the new table
    Dim p_rTableRange As Range      ' Range to place the new table in
    Dim p_intHeaderRow As Integer   ' Header row of the new table
    Dim p_dDate As Date             ' Date for the new table
    
    ' Determine where to place the new week
    p_intHeaderRow = ActiveSheet.UsedRange.Rows.Count + 3           ' After last table with two blank rows in between
    p_dDate = ActiveSheet.Cells(p_intHeaderRow - 6, 1).Value + 7    ' Last date incremented by one week (7 days)
    Set p_rTableRange = Range(Cells(p_intHeaderRow, 1), Cells(p_intHeaderRow + 4, 5))   ' Define range of 5 x 5 table
    
    ' Format range as table
    g_strLastTableName = "Week" & WorksheetFunction.WeekNum(p_dDate)    ' Make table name and store for undo
    ActiveSheet.ListObjects.Add(xlSrcRange, p_rTableRange, , xlYes).Name = g_strLastTableName
    ActiveSheet.ListObjects("Week" & WorksheetFunction.WeekNum(p_dDate)).TableStyle = "TableStyleMedium13"  ' Blue table
    ' Name header row
    Cells(p_intHeaderRow, 1).Formula = "Datum"
    Cells(p_intHeaderRow + 1, 1).Formula = "=R[-7]C+7"      ' Also put date into the table (based on previous date)
    Cells(p_intHeaderRow, 2).Formula = "Onderdeel"
    Cells(p_intHeaderRow, 3).Formula = "Behartiger"
    Cells(p_intHeaderRow, 4).Formula = "Assistent"
    Cells(p_intHeaderRow, 5).Formula = "Raadgevingspunt"
    
    ' Prepare undo
    Set g_wbOldWorkbook = ActiveWorkbook
    Set g_wsOldWorksheet = ActiveSheet
    ' Specify undo routine
    Application.OnUndo "Undo add week", "UndoAddWeek"
    
End Sub

''=======================================================
'' Program:     UndoAddWeek
'' Desc:        Undoes the AddWeek macro by removing the last table generated.
'' Called by:   Directly
'' Call:        UndoAddWeek
'' Arguments:   None
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 23-07-2018   Pieter de Rooij     Initial implementation
''=======================================================
Sub UndoAddWeek()
Attribute UndoAddWeek.VB_Description = "Undoes the add week macro."
Attribute UndoAddWeek.VB_ProcData.VB_Invoke_Func = " \n14"
'
' Undo AddWeek Macro
' Undoes / removes the table generated by the AddWeek macro.
'

'
    ' Variables for the table to be undone
    Dim UndoTable As ListObject
    Dim UndoRange As Range
    
    ' Error handling in case undo does not work for whatever reason
    On Error GoTo Problem
    
    ' Make sure the correct workbook and sheet are active
    g_wbOldWorkbook.Activate
    g_wsOldWorksheet.Activate
    
    ' Remove table
    Set UndoTable = ActiveSheet.ListObjects(g_strLastTableName)
    Set UndoRange = UndoTable.Range
    UndoTable.Unlist
    UndoRange.Delete (xlShiftUp)
    Exit Sub

    ' Error handler
Problem:
    MsgBox "Can't undo"
    
End Sub

''=======================================================
'' Program:     SavePDF
'' Description: Saves the template as PDF.
'' Called by:   Directly
'' Call:        SavePDF()
'' Arguments:   None
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 02-09-2018   Pieter de Rooij     Initial version.
''=======================================================
Sub SavePDF()
'
' SavePDF Macro
' Sla LTV schema op als PDF.
'

'
    ' Save location is current folder \ LTV {month}
    Dim SaveLoc As String
    SaveLoc = ActiveWorkbook.Path & "\LTV " & MonthName(Month(ActiveSheet.Cells(2, 1).Value))     ' Presumes a date is present in A2
    
    ' Do the actual saving
    With ActiveSheet
        ' Set to fit columns on one page
        .PageSetup.FitToPagesWide = 1
        .PageSetup.FitToPagesTall = False
        
        ' Export PDF
        .ExportAsFixedFormat Type:=xlTypePDF, Filename:= _
            SaveLoc & ".pdf", Quality:=xlQualityStandard, _
            IncludeDocProperties:=True, IgnorePrintAreas:=False, _
            OpenAfterPublish:=False
        
        ' Notification
        MsgBox "LTV schema opgeslagen als " & SaveLoc
    End With
    
End Sub

''=======================================================
'' Program:     ConvertAssignments
'' Desc:        Converts a filled 'apply yourself to the field ministry' template to assignments in PDF format.
'' Called by:   Directly (button)
'' Call:        ConvertAssignments
'' Arguments:   None
'' Comments:    This is the main function of automation.
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 13-06-2018   Pieter de Rooij     Formed the stub
'' 04-08-2018   Pieter de Rooij     Elaborated to demonstrate template spawning
'' 09-08-2018   Pieter de Rooij     Complete and working version
'' 10-08-2018   Pieter de Rooij     Fixed template location, delete template page and save result
''=======================================================
Sub ConvertAssignments()
Attribute ConvertAssignments.VB_Description = "Converts the filled in weeks into assignments."
Attribute ConvertAssignments.VB_ProcData.VB_Invoke_Func = " \n14"
    
    ' Ask user where the PDF template is located
'    Dim strTemplLoc As String
'    strTemplLoc = AskUser("Kies de template van toewijzingen")
    
    ' Initialize Adobe modules to edit PDF
    InitializeAdobe
    OpenAdobe (ActiveWorkbook.Path & "\Toewijzingen template.pdf")
    
    ' Loop over weeks for as long as there are filled rows
    g_intRow = 1    ' Start with first row
    While g_intRow < ActiveSheet.UsedRange.Rows.Count
        ConvertWeek
    Wend
    
    ' Delete template page
    DeleteTemplate
    
    ' Ask where to save the result and do so
    Dim strSaveLoc As String
    strSaveLoc = AskUser("Sla toewijzingen op", "Save")
    SaveAdobe (strSaveLoc)
    
    ' Close Adobe modules again
    CloseAdobe
    
    ' Provide feedback that the operation was succesful
    MsgBox "Toewijzingen zijn succesvol aangemaakt en opgeslagen!"
    
End Sub

''=======================================================
'' Program:     AskUser
'' Desc:        Asks the user to select (a) file(s) in the file browser.
''              The function returns a string of the selected file's path,
''              which can be used to open or save.
'' Called by:   ConvertAssignments
'' Call:        AskUser(Title, Type, Multiple)
'' Arguments:   Title       - Window title as string.
''              Type        - Dialog type as string. Can be "save" or "open".
''              Multiple    - Boolean indicating whether or not the user is allowed to select multiple files.
'' Returns:     String of selected file path.
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 03-08-2018   Pieter de Rooij     Initial version
'' 09-08-2018   Pieter de Rooij     Start in current folder
''=======================================================
Function AskUser(ByVal strTitle As String, Optional ByVal strType As String = "Open", Optional ByVal bMulti As Boolean = False) As String
    Dim fdDialog As FileDialog  ' Variable holding the dialog
    
    ' Prepare the dialog
    If LCase(strType) = "open" Then
        ' Open file dialog
        Set fdDialog = Application.FileDialog(msoFileDialogOpen)    ' Store open dialog
        ' Filters can only be set for opening
        fdDialog.Filters.Clear                                      ' Modify file filter
        Call fdDialog.Filters.Add("PDF", "*.pdf")                   ' Only allow PDF files
    ElseIf LCase(strType) = "save" Then
        ' Save file dialog
        Set fdDialog = Application.FileDialog(msoFileDialogSaveAs)  ' Store save dialog
    Else
        MsgBox "Dialog type '" & strType & "' not recognised!"
        Exit Function
    End If
    
    ' Specify dialog and show to user
    fdDialog.Title = strTitle                                   ' Specify window title
    fdDialog.AllowMultiSelect = bMulti                          ' Allow to choose multiple files or not
    fdDialog.InitialFileName = ActiveWorkbook.Path & "\*.pdf"   ' Start in current folder
    
    ' Show dialog
    If fdDialog.Show Then
        ' File(s) picked, decide what to do
        If fdDialog.SelectedItems.Count > 1 Then
            MsgBox "Multiple files not implemented yet!"
            Exit Function
        Else
            ' One file chosen, return its path as string
            AskUser = fdDialog.SelectedItems(1)
        End If
    Else
        ' User cancelled, return empty string
        AskUser = ""
    End If
    
End Function

''=======================================================
'' Program:     ConvertWeek
'' Desc:        Converts a week in the template into assignments.
'' Called by:   ConvertAssignments
'' Call:        ConvertWeek
'' Arguments:   None
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 04-08-2018   Pieter de Rooij     Formed the stub (just increment row)
'' 07-08-2018   Pieter de Rooij     Actually reads a week
''=======================================================
Sub ConvertWeek()
    ' Check whether the current row contains something to read
    If InStr(LCase(ActiveSheet.Cells(g_intRow, 1).Value), "datum") > 0 Then
        ' Header row found, read week
        Dim dDate As Date   ' Stores this week's date
        If Not IsDate(ActiveSheet.Cells(g_intRow + 1, 1).Value) Then
            ' No date found, ask the user what to do
            Dim bUserAns As Boolean
            bUserAns = MsgBox("No date found at A" & g_intRow + 1 & "! Skip week (No to abort)?", vbYesNo + vbQuestion, "Convert week")
            If bUserAns Then
                ' User is fine with skipping week, so go to next row
                g_intRow = g_intRow + 1
            End If
            ' Function can be ended, no matter what the user selected
            Exit Sub
        End If
        
        ' Store date and convert every row
        dDate = ActiveSheet.Cells(g_intRow + 1, 1).Value
        ' Every week consists of four rows (types of assignments), convert each of them
        Dim idx As Integer  ' Loop index
        For idx = 1 To 4
            ConvertRow dDate, g_intRow + idx
        Next idx
        
        ' Lastly advance 7 rows, because that is where the next week ought to start
        g_intRow = g_intRow + 7
        
    Else
        ' Just go to the next row
        g_intRow = g_intRow + 1
    End If
    
    ' #Falls back to ConvertAssignments which in turn calls this function for coming rows #
    
End Sub

''=======================================================
'' Program:     ConvertRow
'' Desc:        Turns a row within a week table into assignments.
'' Called by:   ConvertWeek
'' Call:        ConvertRow(WeekDate)
'' Arguments:   WeekDate    - Date to be put on the assignment(s)
'' Comments:    None
'' Changes----------------------------------------------
'' Date         Programmer          Change
'' 04-08-2018   Pieter de Rooij     Formed the stub
'' 07-08-2018   Pieter de Rooij     Actually reads a row
''=======================================================
Sub ConvertRow(ByVal dAsDate As Date, ByVal intRRow As Integer)
    ' Store table row in variable
    Dim rRow As Range
    Set rRow = ActiveSheet.Range(Cells(intRRow, 1), Cells(intRRow, 5))
    
    ' Determine whether or not and how many assignments are required
    If Not IsEmpty(ActiveSheet.Cells(intRRow, 4).Value) Then
        ' If there is an assistant, two assignments have to be written
        WriteAssignment rRow.Cells(1, 3).Value, dAsDate, rRow.Cells(1, 2).Value, rRow.Cells(1, 5).Value, rRow.Cells(1, 4).Value, 1  ' Student
        WriteAssignment rRow.Cells(1, 3).Value, dAsDate, rRow.Cells(1, 2).Value, rRow.Cells(1, 5).Value, rRow.Cells(1, 4).Value, 2  ' Assistant
    ElseIf IsEmpty(ActiveSheet.Cells(intRRow, 5).Value) Then
        ' If it is just a discussion of a presentation (no counsel), no assignment is required
        Exit Sub
    Else
        ' Otherwise one assignment is sufficient
        WriteAssignment rRow.Cells(1, 3).Value, dAsDate, rRow.Cells(1, 2).Value, rRow.Cells(1, 5).Value
    End If
    
End Sub
